;;-*- mode: kahua; coding: utf-8 -*-
;; Kahua website macro handling
;;
;;  Copyright (c) 2004-2007 Scheme Arts, L.L.C., All rights reserved.
;;  Copyright (c) 2004-2007 Time Intermedia Corporation, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: rss.kahua,v 1.8 2007/05/22 14:42:02 bizenn Exp $

(use rfc.uri)

(define (page-url page)
  (and-let* ((pd (find-kahua-instance <page-data> page)))
    (page-data-url pd)))

(define (page-data-url page-data)
  (apply kahua-self-uri-full
	 "show" (map uri-encode-string
		     (pagename-split (slot-ref page-data 'name)))))

(define (page-title page)
  (and-let* ((pd (find-kahua-instance <page-data> page)))
    (page-data-title pd)))

(define (page-data-title page-data)
  (base-pagename (slot-ref page-data 'name)))

(define (pubdate-mtime page)
  (and-let* ((pd (find-kahua-instance <page-data> page)))
    (page-data-mtime pd)))

(define (page-data-mtime page-data)
  (time->local-time-string (slot-ref page-data 'mtime)))

(define (time->local-time-string t)
  (sys-strftime "%a, %d %b %Y %H:%M:%S %z" (sys-localtime t)))

(define-entry (rss)
  (let1 entries (or (and-let* ((rc (find-kahua-instance <page-set> "recent-changes")))
		      (take* (slot-ref rc 'pages) 15))
		    '())
    `((rss (@ (version "2.0"))
           (channel
            (title ,*kahua-web-title*)
            (link  ,(kahua-self-uri-full "show"))
            (description "Recent changes...")
            (lastBuildDate ,(time->local-time-string (sys-time)))
	    ,@(if (pair? entries)
		  `((pubDate ,(pubdate-mtime (car entries))))
		  '())
	    (docs "http://blogs.law.harvard.edu/tech/rss")
	    (generator ,#`",(kahua-worker-type) ,|*kahua-web-version*|")
            ,@(filter-map (lambda (e)
			    (and-let* ((pd (find-kahua-instance <page-data> e)))
			      `(item
				(title ,(page-data-title pd))
				(link ,(page-data-url pd))
				(pubDate ,(page-data-mtime pd)))))
			  entries))))))

(define (write-rss)
  (kahua-write-static-file
   #`",(kahua-worker-type)/rss.xml" (rss) '()))

(when (find (pa$ string=? "write-rss") (kahua-app-args))
  (%kahua-web-rss-uri% (cut kahua-static-document-url #`",(kahua-worker-type)/rss.xml"))
  (add-hook! save-page-hook write-rss))
