;;-*-Scheme-*-
;; Kahua website
;;
;;  Copyright (c) 2004 Scheme Arts, L.L.C., All rights reserved.
;;  Copyright (c) 2004 Time Intermedia Corporation, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: login.kahua,v 1.4 2006/09/26 08:02:04 bizenn Exp $

(define non-member-edit-flag
  (and-let* ((args (kahua-app-args))
             (_ (not (null? args))))
    (string-ci=? (car args) "non-member-edit")))
             
;; login account can only be issued by "admin" role.

(define (login-link)
  (let1 page-path (or (and-let* ((page (current-page)))
                        (pagename-split (ref page 'key)))
                      '())
    (or (and-let* ((name (kahua-current-user-name)))
          `((span (@ (class "login-link"))
                  "User '" (a/cont (@@ (cont ,user-admin ,name)) ,name) "' "
                  (a/cont (@@ (cont ,logout ,@page-path)) "[logout]"))))
        `((span (@ (class "login-link"))
                (a/cont (@@ (cont ,login-form ,@page-path)) "[login]"))))))

(define-entry (logout :rest page-path)
  (if non-member-edit-flag
      (parameterize ((current-db #f))
	(set! (kahua-current-user-name) #f))
      (set! (kahua-current-user-name) #f))
  (set! (kahua-current-user) #f)
  ;; if we're in the persistent page, stay on the same page.
  ;; otherwise, redirect to the top.
  (if (and (current-page) (ref (current-page) 'persistent-id))
      (standard-page (current-page))
      (redirect-to (get-page "/"))))

(define-entry (login-form :rest return-page-path)
  (define (show-form extra-contents logname pass)
    (standard-page
     (make <page>
       :title "Login" :key "/"
       :content
       `((h1 "Login")
         ,@extra-contents
         (form/cont
          (@@ (cont ,login-check (logname) (pass)))
          (table
           (@ (class "user-form"))
           (tr (th "Username")
               (td (input (@ (type "text") (name "logname")
                             (value ,(or logname ""))))))
           (tr (th "Password")
               (td (input (@ (type "password") (name "pass")
                             (value ,(or pass ""))))))
           (tr (th)
               (td (input (@ (type "submit") (name "submit")
                             (value "Login")))))))
         ))))
  (define (show-form-bis extra-contents logname pass)
    (standard-page
     (make <page>
       :title "Login" :key "/"
       :content
       `((h1 "Login")
         ,@extra-contents
         (form/cont
          (@@ (cont ,login-check-bis (logname)))
          (table
           (@ (class "user-form"))
           (tr (th "Nickname")
               (td (input (@ (type "text") (name "logname")
                             (value ,(or logname ""))))))
           (tr (th)
               (td (input (@ (type "submit") (name "submit")
                             (value "Login")))))))
         ))))
  (define login-check
    (entry-lambda (:keyword logname pass)
      (if (kahua-check-user logname pass)
        (begin (set! (kahua-current-user) logname)
               (redirect-to (get-page (pagename-join return-page-path))))
        (show-form
         '((p (@ (class "warning"))
              "Username or password does not match.  Try again."))
         logname pass))))
  (define login-check-bis
    (entry-lambda (:keyword logname)
      (cond ((kahua-find-user logname)
             (show-form
              '((p (@ (class "warning"))
                   "You are registered user. Please enter password."))
              logname #f))
            ((string-null? logname)
             (show-form-bis
              '((p (@ (class "warning"))
                   "Please enter your nickname"))
              #f #f))
            (else
             (begin
	       (parameterize ((current-db #f))
		 (set! (kahua-current-user-name) logname))
               (redirect-to (get-page (pagename-join return-page-path))))))))
  (if non-member-edit-flag
      (show-form-bis '() #f #f)
      (show-form '() #f #f)))

;; user info page
;;   admin role or developer role can access all user page.
;;   other users can only access his/her own page
(define-entry (user-admin logname)
  (let1 user (find-kahua-instance <kahua-user> logname)
    (define (user-page extra-message)
      (standard-page
       (make <page>
         :title #`"User ',logname'" :key "/"
         :content
         `((h1 "User '",logname"'")
           (p "User " ,logname " has role(s): "
              ,(string-join (map x->string (ref user 'role-alist)) ", "))
           (h3 "Change password:")
           (form/cont
            (@@ (cont ,change-pass (oldpass) (newpass1) (newpass2)))
            ,@extra-message
            (table
             (@ (class "user-form"))
             (tr (th "Old password")
                 (td (input (@ (type "password") (name "oldpass")))))
             (tr (th "New password")
                 (td (input (@ (type "password") (name "newpass1")))))
             (tr (th "Retype new password")
                 (td (input (@ (type "password") (name "newpass2")))))
             (tr (th)
                 (td (input (@ (type "submit") (name "submit")
                               (value "Change")))))))
           ))))
    (define change-pass
      (entry-lambda (:keyword oldpass newpass1 newpass2)
        (cond ((not (equal? newpass1 newpass2))
               (user-page '((p (@ (class "warning"))
                               "New passwords don't match."))))
              ((not (kahua-user-password-change user oldpass newpass1))
               (user-page '((p (@ (class "warning"))
                               "Wrong password."))))
              (else
               (kahua-db-sync)
               (user-page '((p "Password changed.")))))))

    (if (or (kahua-user-has-role? (kahua-current-user) '(admin developer))
            (and-let* ((u (kahua-current-user)))
              (equal? logname (ref u 'login-name))))
      (user-page '())
      (standard-page
       (make <page>
         :title #`"User ,logname" :key "/"
         :content
         `((p (@ (class "warning"))
              "You don't have a priviledge to browse this page.")))))
    ))
