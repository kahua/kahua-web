;;-*- mode: kahua; coding: utf-8 -*-
;; Kahua website
;;
;;  Copyright (c) 2004-2007 Scheme Arts, L.L.C., All rights reserved.
;;  Copyright (c) 2004-2007 Time Intermedia Corporation, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: login.kahua,v 1.15 2007/06/04 12:46:02 bizenn Exp $

(use srfi-1)
(use gauche.collection)

(define non-member-edit-flag
  (and-let* ((args (kahua-app-args))
	     ((pair? args)))
    (string-ci=? (car args) "non-member-edit")))

(define show-login-form
  (letrec (;; Allow anonymous login
	   (show-anon-login-form
	    (lambda (extra-contents page)
	      (define (make-login-check-entry)
		(entry-lambda (:keyword logname)
		  (cond ((kahua-find-user logname)
			 (show-normal-login-form
			  '((p (@ (class "warning"))
			       "You are registered user. Please enter password."))
			  page))
			((string-null? logname)
			 (show-anon-login-form
			  '((p (@ (class "warning"))
			       "Please enter your nickname"))
			  page))
			(else
			 (parameterize ((current-db #f))
			   (set! (kahua-current-user-name) logname))
			 (redirect-to page)))))
	      (standard-page
	       (make <page> :title "Login" :key "/"
		     :content
		     `((h2 "Login") ,@extra-contents
		       (form/cont
			(@@ (cont ,(make-login-check-entry)))
			(table
			 (@ (class "user-form"))
			 (tr (th "Nickname") (td ,(make-textbox "logname")))
			 (tr (th) (td ,(make-submit "Login"))))))))))
	   ;; Only normal login
	   (show-normal-login-form
	    (lambda (extra-contents page)
	      (define (make-login-check-entry)
		(entry-lambda (:keyword logname pass)
		  (if (kahua-login logname pass)
		      (redirect-to page)
		      (show-normal-login-form
		       '((p (@ (class "warning"))
			    "Username or password does not match.  Try again."))
		       page))))
	      (standard-page
	       (make <page>
		 :title "Login" :key "/"
		 :content
		 `((h2 "Login")
		   ,@extra-contents
		   (form/cont
		    (@@ (cont ,(make-login-check-entry)))
		    (table
		     (@ (class "user-form"))
		     (tr (th "Username") (td ,(make-textbox "logname")))
		     (tr (th "Password") (td ,(make-password-box "pass")))
		     (tr (th) (td ,(make-submit "Login")))))))))))
    (if non-member-edit-flag
	show-anon-login-form
	show-normal-login-form)))

(define kahua-web-authorized?
  (if non-member-edit-flag
      (lambda roles
	(if (null? roles)
	    (and-let* ((name (kahua-current-user-name)))
	      (and (not (string-null? name))
		   name))
	    (and (apply kahua-authorized? roles)
		 (kahua-current-user-name))))
      (lambda roles
	(and (apply kahua-authorized? roles)
	     (kahua-current-user-name)))))

(define (kahua-web-current-path)
  (string-join (cons (kahua-current-entry-name)
		     (kahua-context-ref "x-kahua-path-info"))
	       "/"))

(define (with-kahua-web-authorization thunk . roles)
  (if (apply kahua-web-authorized? roles)
      (thunk)
      (show-login-form `((p (@ (class "warning"))
			    "Login required: "))
		       (kahua-web-current-path))))

(define (standard-page/auth page)
  (if (kahua-web-authorized?)
      (standard-page page)
      (show-login-form `((p (@ (class "warning"))
			    "Login required: "))
		       (kahua-web-current-path))))
             
;; login account can only be issued by "admin" role.

(define (login-link)
  (define (make-login-entry page)
    (lambda () (show-login-form '() page)))
  (define (make-logout-entry page)
    (lambda ()
      (when non-member-edit-flag
	(parameterize ((current-db #f)) (set! (kahua-current-user-name) #f)))
      (kahua-logout)
      (redirect-to page)))
  (or (and-let* ((name (kahua-current-user-name)))
	`((span (@ (class "login-link"))
		"User '" (a/cont (@@ (cont ,user-admin ,name)) ,name) "' "
		(a/cont (@@ (cont ,(make-logout-entry (kahua-web-current-path)))) "[logout]"))))
      `((span (@ (class "login-link"))
	      (a/cont (@@ (cont ,(make-login-entry (kahua-web-current-path)))) "[login]")))))

;; user info page
;;   admin role or developer role can access all user page.
;;   other users can only access his/her own page
(define-constant *developer-role* 'developer)
(define-constant *administrator-role* 'admin)
(define-constant *allowed-roles* `(,*administrator-role* ,*developer-role*))

(define-entry (user-admin logname)
  (cond ((not logname)
	 (if (kahua-authorized? *administrator-role*)
	     (user-list-page '())
	     (dont-have-priviledge-page "")))
	((kahua-find-user logname)
	 => (lambda (u)
	      (cond ((eq? u (kahua-current-user)) (self-page u '()))
		    ((kahua-authorized? *administrator-role*) (user-page u '() '()))
		    (else (dont-have-priviledge-page logname)))))
	(else (standard-page
	       (make <page>
		 :title #`"Unknown user: ,|logname|" :key "/"
		 :content `((p (@ (class "warning"))
			       "Unknown user: " ,logname)))))))

(define (self-page user extra-message)
  (define (change-pass-entry)
    (entry-lambda (:keyword oldpass newpass1 newpass2)
      (cond ((not (equal? newpass1 newpass2))
	     (self-page user '((p (@ (class "warning"))
				  (strong "New passwords don't match.")))))
	    ((not (kahua-user-password-change user oldpass newpass1))
	     (self-page user '((p (@ (class "warning"))
				  (strong "Wrong password.")))))
	    (else (self-page user '((p (strong "Password changed!!"))))))))
  (let1 logname (slot-ref user 'login-name)
    (standard-page
     (make <page>
       :title #`"User: ',logname'" :key "/"
       :content
       `(,@(if (kahua-authorized? 'admin)
	       `((div (@ (id "edit-menu"))
		      (a/cont (@@ (cont ,user-admin)) "[User List]")))
	       '())
	 (h2 "User: " ,logname)
	 (p "Role(s): " ,(string-join (map x->string (ref user 'role-alist)) ", "))
	 (h3 "Change password:")
	 (form/cont
	  (@@ (cont ,(change-pass-entry)))
	  ,@extra-message
	  (table
	   (@ (class "user-form"))
	   (tr (th "Old password:") (td ,(make-password-box "oldpass")))
	   (tr (th "New password:") (td ,(make-password-box "newpass1")))
	   (tr (th "Again new password:") (td ,(make-password-box "newpass2")))
	   (tr (th) (td ,(make-submit "Change"))))))
       ))))

(define (user-page user chpw-message chrls-message)
  (define (reset-pass-entry)
    (entry-lambda (:keyword newpass1 newpass2)
      (cond ((not (equal? newpass1 newpass2))
	     (user-page user `((p (@ (class "warning"))
				  (strong "New passwords don't match."))) '()))
	    ((kahua-user-password-change-force user newpass1)
	     (user-page user '((p (strong "Password changed!!"))) '()))
	    (else
	     (user-page user `((p (@ (class "warning"))
				  (strong "Cannot change password."))) '())))))
  (define (set-roles-entry)
    (entry-lambda (:mvkeyword roles)
      (slot-set! user 'role-alist
		 (lset-intersection eq? (map string->symbol roles) *allowed-roles*))
      (user-page user '() '((p (strong "Role(s) set"))))))
  (let ((logname (slot-ref user 'login-name))
	(uroles  (slot-ref user 'role-alist)))
    (standard-page
     (make <page>
       :title #`"User: ,|logname|" :key "/"
       :content
       `((div (@ (id "edit-menu"))
	      (a/cont (@@ (cont ,user-admin)) "[User List]"))
	 (h2 "User: " ,logname)
	 (div (@ (id "roles-change"))
	      ,@chrls-message
	      (form/cont (@@ (cont ,(set-roles-entry)))
			 (p "Role(s): " ,@(make-roles-checklist "roles" uroles)
			    ,(make-submit "Change"))))
	 (h3 "Change password:")
	 (div (@ (id "password-change"))
	      ,@chpw-message
	      (form/cont (@@ (cont ,(reset-pass-entry)))
			 (table
			  (@ (class "user-form"))
			  (tr (th "New password:") (td ,(make-password-box "newpass1")))
			  (tr (th "Again new password:") (td ,(make-password-box "newpass2")))
			  (tr (th) (td ,(make-submit "Change")))))))
       ))))

(define (user-list-page extra-message)
  (define (change-activity-entry u inactive?)
    (lambda ()
      (slot-set! u 'inactive inactive?)
      (redirect/cont (cont user-admin))))
  (standard-page
   (make <page>
     :title #`"Registered Users" :key "/"
     :content
     `((h2 "Registered Users")
       (p (a/cont (@@ (cont ,(lambda () (new-user-form '())))) "[New user]"))
       (table (@ (class "user-list"))
	      (thead
	       (tr (th "Login name") (th "Roles")
		   (th (@ (colspan 2)) "Activity")))
	      (tbody
	       ,@(map (lambda (u)
			(let ((login-name (name-of u))
			      (inactive? (inactive? u)))
			  `(tr (td (a/cont (@@ (cont ,user-admin ,login-name))
					   ,login-name))
			       (td ,(string-join (map symbol->string (roles-of u)) ", "))
			       (td ,(if inactive? "NO" "YES"))
			       (td ,(if (eq? u (kahua-current-user))
					(if inactive? "[Activate]" "[Deactivate]")
					`(a/cont (@@ (cont ,(change-activity-entry
							     u (not inactive?))))
						 ,(if inactive? "[Activate]" "[Deactivate]"))))
			       )))
		      (sort (coerce-to <list> (make-kahua-collection (kahua-current-user-class)))
			    (lambda (a b) (string<? (name-of a) (name-of b)))))))))))

(define (new-user-form . extra-messages)
  (define (add-user-entry)
    (entry-lambda (:keyword login-name password1 password2 :mvkeyword roles)
      (cond ((or (not login-name) (> 3 (string-length login-name)))
	     (new-user-form (make-warning "login-name's length must be 3 or longer.")))
	    ((kahua-find-user login-name)
	     (new-user-form (make-warning "User '" login-name "' already exists.")))
	    ((or (not password1) (> 4 (string-length password1)))
	     (new-user-form (make-warning "Password must be longer than 3.")))
	    ((not (equal? password1 password2))
	     (new-user-form (make-warning "Passwords don't match.")))
	    (else
	     (kahua-add-user login-name password1 :role-alist
			     (lset-intersection eq? (map string->symbol roles) *allowed-roles*))
	     (redirect/cont (cont user-admin))))))
  (standard-page
   (make <page>
     :title "New user" :key "/"
     :content
     `((div (@ (id "edit-menu"))
	    (a/cont (@@ (cont ,user-admin)) "[User List]"))
       (h2 "New user")
       (div
	,@extra-messages
	(form/cont
	 (@@ (cont ,(add-user-entry)))
	 (table (@ (class "user-form"))
		(tr (th "Login name:") (td ,(make-textbox "login-name")))
		(tr (th "Role(s):")    (td ,@(make-roles-checklist "roles" '())))
		(tr (th "Password:")   (td ,(make-password-box "password1")))
		(tr (th "Again password:") (td ,(make-password-box "password2")))
		(tr (th) (td ,(make-submit "Add user"))))))))))

(define (dont-have-priviledge-page login-name)
  (standard-page
   (make <page>
     :title #`"User: ,|login-name|" :key "/"
     :content
     `((p (@ (class "warning"))
	  "You don't have a priviledge to browse this page.")))))

(define (make-textbox name . attrs)
  `(input (@ (type "text") (name ,name) ,@attrs)))

(define (make-textarea name value . attrs)
  `(textarea (@ (name ,name) ,@attrs) ,(if value value "")))

(define (make-password-box name)
  `(input (@ (type "password") (name ,name))))

(define (make-checkbox name value label checked? . attrs)
  `(label (input (@ (type "checkbox") (name ,name) (value ,value)
		    ,@(if checked? '((checked "checked")) '()) ,@attrs))
	  ,label))

(define (make-submit value)
  `(input (@ (type "submit") (value ,value))))

(define (make-roles-checklist name roles)
  (map (lambda (r)
	 (let1 rstr (symbol->string r)
	   (make-checkbox name rstr rstr (memq r roles))))
       *allowed-roles*))

(define (make-filebox name)
  `(input (@ (type "file") (name ,name))))

(define (make-warning . texts)
  `(p (@ (class "warning"))
      (strong ,@texts)))
