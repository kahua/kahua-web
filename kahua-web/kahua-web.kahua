;;-*- mode: kahua; coding: utf-8 -*-
;; Kahua website
;;
;;  Copyright (c) 2004-2007 Scheme Arts, L.L.C., All rights reserved.
;;  Copyright (c) 2004-2007 Time Intermedia Corporation, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: kahua-web.kahua,v 1.87 2007/07/03 13:30:30 bizenn Exp $

(use srfi-1)
(use srfi-2)
(use srfi-13)
(use file.util)
(use util.list)
(use rfc.uri)
(use gauche.logger)
(use gauche.sequence)
(use gauche.parameter)
(use gauche.hook)
(use gauche.threads)
(use wiliki.format)
(if (global-variable-bound? (current-module) 'wiliki:current-page)
    (define wiliki-current-page wiliki:current-page)
    (eval '(use wiliki.page) (current-module)))
(use wiliki.log)
(use sxml.serializer)

(use history)

(use rss-reader)

(log-open (kahua-logpath "kahua-web.log"))

(define save-page-hook (make-hook))
(define %kahua-web-rss-uri% (make-parameter  (cut kahua-self-uri-full "rss")))
(define (kahua-web-rss-uri)
  ((%kahua-web-rss-uri%)))
(define kahua-web-charset (gauche-character-encoding))
;; Set Account Number
(define *google-analytics-tracking* (make-parameter #f))
;; RSS has full description or no description
(define *rss-with-full-description* (make-parameter #f))

(load "kahua-web/version.kahua")
(load "kahua-web/macro.kahua")
(load "kahua-web/login.kahua")
(load "kahua-web/upload.kahua")
(load "kahua-web/search.kahua")
(load "kahua-web/comment.kahua")
(load "kahua-web/rss.kahua")
(load "kahua-web/history.kahua")
(load "kahua-web/local.kahua")

(define-syntax define-if-not-bound
  (syntax-rules ()
    ((_ var expr)
     (define var (if (global-variable-bound? (current-module) 'var) var expr)))))

(define-if-not-bound *kahua-web-title* "kahua-web")

(define-if-not-bound *kahua-web-email*
  "kahua-web@example.org")

;; Initialization for history file of WiLiKi module.
(define-constant *history-file* "wikidata.log")
(history-file (delay (cond ((and (slot-exists? (kahua-config) 'data-directory)
				 (file-is-directory? (kahua-data-directory)))
			    (make-directory* (kahua-datapath "kahua-web"))
			    (kahua-datapath "kahua-web" *history-file*))
			   ((and-let* ((db (current-db))
				       ((slot-exists? db 'real-path)))
			      (build-path (slot-ref db 'real-path) *history-file*)))
			   (else
			    (let1 d (build-path (kahua-database-directory) "kahua-web")
			      (make-directory* d)
			      (build-path d *history-file*))))))

;;========================================================
;; Application error handling
;;
;;   An error that causes a normal occasion (e.g. input error)
;;   can be thrown by <kahua-web-error> object.
;;   The <error> would be considered as a program error.

(define-condition-type <kahua-web-error> <exception> kahua-web-error?
  (form form-of))

(define (kahua-web-error form)
  (error <kahua-web-error> :form form))

;;========================================================
;; Page-data
;;

;; NB: there's no way to delete a <page-data>.  A 'deleted' pages
;; will be marked as "hidden".

(define-class <page-data> (<kahua-persistent-base>)
  ((name       :allocation :persistent :init-keyword :name
	       :index :unique)
   (content    :allocation :persistent :init-keyword :content
               :init-value "")
   (ctime      :allocation :persistent :init-keyword :ctime
               :init-form (sys-time))
   (cuser      :allocation :persistent :init-keyword :cuser
               :init-value #f)
   (mtime      :allocation :persistent :init-keyword :mtime
               :init-value #f)
   (muser      :allocation :persistent :init-keyword :muser
               :init-value #f)
   (hidden     :allocation :persistent :init-keyword :hidden
               :init-value #f)
   (title-pane :allocation :persistent :init-keyword :title-pane
	       :init-value #f)
   (side-pane  :allocation :persistent :init-keyword :side-pane
	       :init-value #f)
   (bottom-pane :allocation :persistent :init-keyword :bottom-pane
		:init-value #f)
   (comment    :allocation :persistent :init-keyword :comment
	       :init-value #f)
   ))

;; [<page-data>]
(define (all-page-data)
  (coerce-to <list> (make-kahua-collection
		     <page-data>
		     :predicate (lambda (x) (not (slot-ref x 'hidden))))))

;; pagename -> Maybe <page-data>
;; pagename must be canonicalized.
(define (get-page-data pagename)
  (and-let* ((o (find-kahua-instance <page-data> 'name pagename)))
    (and (not (slot-ref o 'hidden)) o)))

(define (page-data-exists? pagename)
  (not (not (get-page-data pagename)))) ;; for now

(define (get-page-data-from-id id)
  (find-kahua-instance <page-data> id))

(define-method pagename<? ((a <page-data>) (b <page-data>))
  (string<? (ref a 'name) (ref b 'name)))

;;========================================================
;; Page
;;

;; <page> class is used to represent any "displayable" pages, including
;; procedurally constructed page.
;; For the page that has persistent <page-data>, persistent-id slot
;; containts <page-data>'s id.
(define-class <page> (<wiliki-page>)
  ((persistent-id :init-keyword :persistent-id :init-value #f)
   (virtual-pages :allocation :class :init-value '())
   (title-pane :init-keyword :title-pane :init-value #f)
   (side-pane :init-keyword :side-pane :init-value #f)
   (bottom-pane :init-keyword :bottom-pane :init-value #f)))

(define (persistent-page? page)
  (and (is-a? page <page>) (ref page 'persistent-id)))

(define (add-virtual-page! pagename proc)
  (class-slot-set! <page> 'virtual-pages
                   (assoc-set! (class-slot-ref <page> 'virtual-pages)
                               pagename proc)))

(define (lookup-virtual-page pagename)
  (assoc-ref (class-slot-ref <page> 'virtual-pages) pagename))

;; current page
;; NB: this is different from wiliki:current-page, which contains
;; the page being currently formatted.  This current-page keeps
;; the "toplevel" page to be shown.
(define current-page (make-parameter #f))

;; javascript
(define replace-footnotes-js "
window.onload = function () {
    var anks = document.getElementsByTagName('a');
    var footnotes = new Array();
    for (i=0; i<anks.length; i++) {
	var aClass = anks[i].getAttribute('class');
	if (aClass == 'footnotelink') {
	    footnotes.push(anks[i]);
	}
    }
    var footElem = document.getElementById('footnote');
    if (footnotes.length > 0) {
	footElem.appendChild(document.createElement('hr'));
	for (i=0; i<footnotes.length; i++) {
	    // body
	    var ref = 'reference-' + (i + 1);
	    var foot = 'footnote-' + (i + 1);
	    footnotes[i].setAttribute('id', ref);
	    footnotes[i].setAttribute('href', '#' + foot);
	    var footnoteText = footnotes[i].firstChild;
	    footnotes[i].replaceChild(document.createTextNode('[' + (i + 1) + ']'), footnoteText);
	    // footnote
	    var footnoteBody = document.createElement('div');
	    footnoteBody.setAttribute('id', foot);
	    footnoteBody.setAttribute('class', 'footnote');
            var sup = document.createElement('sup');
            footnoteBody.appendChild(sup);
	    var upLink = document.createElement('a');
	    upLink.setAttribute('href', '#' + ref);
	    upLink.appendChild(document.createTextNode('^' + (i + 1)));
	    sup.appendChild(upLink);
	    footnoteBody.appendChild(footnoteText);
	    footElem.appendChild(footnoteBody);
	}
	footElem.appendChild(document.createElement('hr'));
    }
}")

;; <page-data> -> <page>
(define (page-data->page data)
  (make <page>
    :title (base-pagename (ref data 'name))
    :key   (slot-ref data 'name)  :persistent-id (key-of data)
    :extra-head-elements (script/ (@/ (type "text/javascript")) replace-footnotes-js)
    :content (slot-ref data 'content)
    :ctime (slot-ref data 'ctime) :cuser (slot-ref data 'cuser)
    :mtime (slot-ref data 'mtime) :muser (slot-ref data 'muser)
    :title-pane (slot-ref data 'title-pane)
    :side-pane (slot-ref data 'side-pane)
    :bottom-pane (slot-ref data 'bottom-pane)))

;; pagename -> Maybe <page>
;; pagename must be canonicalized.
(define (get-page pagename)
  (cond ((not pagename) #f) ;; for safety
        ((lookup-virtual-page pagename) => (cut <> pagename))
        ((get-page-data pagename) => page-data->page)
        (else #f)))

;; save page.
(define (save-page page cont comment)
  (define (get-page-data/check id)
    (let* ((data (get-page-data-from-id id))
           (newname (slot-ref page 'key))
           (oldname (slot-ref data 'name)))
      (unless (equal? oldname newname)
        ;; page name is changed.  make sure it doesn't conflict.
        (let1 other (get-page-data newname)
          (when (and other (not (equal? (kahua-persistent-id data) (kahua-persistent-id other))))
            (slot-set! page 'key oldname)
            (kahua-web-error (duplicated-page-name newname))))
        (slot-set! data 'name newname))
      data))
  (define (get-new-page/check name)
    (let1 other (get-page-data name)
      (when other (kahua-web-error (duplicated-page-name name)))
      (make <page-data> :name (slot-ref page 'key))))
  (define (duplicated-page-name name)
    (edit-page page
               `(p (@ (class "warning"))
                   "A page with name \"" ,name "\" already exists. "
                   "Use a different name.")))

  (if (kahua-web-authorized?)
      (let ((data (cond ((slot-ref page 'persistent-id) => get-page-data/check)
                        (else (get-new-page/check (slot-ref page 'key)))))
            (new-content (slot-ref page 'content)))
        (when (slot-ref page 'persistent-id)
          (write-log (slot-ref page 'persistent-id)
                     (slot-ref data 'content)
                     new-content
                     (or comment "")
                     (kahua-current-user-name)))
        (slot-set! data 'content new-content)
	(slot-set! data 'title-pane (slot-ref page 'title-pane))
	(slot-set! data 'side-pane (slot-ref page 'side-pane))
	(slot-set! data 'bottom-pane (slot-ref page 'bottom-pane))
	(slot-set! data 'comment
		   (and-let* ((comment)
			      (c (string-trim-both comment))
			      ((not (string-null? c))))
		     c))
        (unless (slot-ref page 'persistent-id)
          (slot-set! data 'cuser (kahua-current-user-name)))
        (slot-set! data 'mtime (sys-time))
        (slot-set! data 'muser (kahua-current-user-name))
        (if (slot-ref page 'persistent-id)
            (cont page)
            (cont (page-data->page (get-page-data (slot-ref page 'key))))))
      (let1 epage (make-error-page "Error: Not login"
				   `(p (@ (class "warning"))
				       "To edit a page you have to login!"))
	(standard-page epage))))

(define (front-page? page)
  (equal? (ref page 'key) "/"))

;;========================================================
;; Page set
;;

(define-class <page-set> (<kahua-persistent-base>)
  ((name       :init-keyword :name :init-value #f
               :allocation :persistent :index :unique)
   (max-pages  :init-keyword :max-pages :init-value #f
               :allocation :persistent)
   (pages      :init-keyword :pages :init-value '()
               :allocation :persistent)
   ))

(define-method key-of ((self <page-set>))
  (x->string (ref self 'name)))

;; assume pagename is canonicalized
(define-method page-set-push-unique! ((self <page-set>) (page <page>))
  (let ((pid (slot-ref page 'persistent-id))
	(terminate? (if (slot-ref self 'max-pages)
			(lambda (_ s) (null? s))
			(lambda (c s) (or (<= c 1) (null? s))))))
    (slot-set! self 'pages
	       (let loop ((ls (list pid))
			  (count (slot-ref self 'max-pages))
			  (src (slot-ref self 'pages)))
		 (if (terminate? count src)
		     (reverse! ls)
		     (let1 p (car src)
		       (if (equal? pid p)
			   (loop ls count (cdr src))
			   (loop (cons p ls) (- count 1) (cdr src)))))))))

;; (String, Bool=#f, MaxPages=#f) -> <page-set>
;; second arg specifies whether it should create a new page set if
;; the named one doesn't exist.
(define (get-page-set page-set-name . opts)
  (let-optionals* opts ((create? #f) (maxpages #f))
    (or (find-kahua-instance <page-set> 'name page-set-name)
        (and create?
             (make <page-set>
               :name page-set-name
               :max-pages maxpages)))))

;; Default page sets.  recent-changes and news
(define (page-set:recent-changes) (get-page-set "recent-changes" #t 50))
(define (page-set:news)           (get-page-set "news" #t 50))

;;========================================================
;; Page paths
;;

;; Kahua-web's pages can be constructed in hierarchy.
;; Each page has a name beginning with "/", and have similar
;; structure like pathnames.
;; Within the page, wikiname can refer to the other pages
;; by "relative pathname".
;;
;; Note that the page is not a "directory"---there's no real
;; container in the database, but only a conceptual group of
;; names.   An unqualified name refers to the page in the
;; same conceptual directory---that is, "baz" in the page
;; "/foo/bar" means "/foo/baz", not "/foo/bar/baz".
;;
;; The pathname component "." and ".." can be used to point relative
;; names.  Note that these also works from the conceptual directory
;; where the page is in, so "./baz" in the page "/foo/bar" is
;; "/foo/baz", and "../baz" in the page "/foo/bar" is "/baz".
;;
;; A special name "$" refers to the page itself.  So "$/baz" in the
;; page "/foo/bar" means "/foo/bar/baz".
;;
;; The name of the top page is "/".
;; The "parent" page of the top page is the top page itself.
;; That is, "/a/../.." refers to "/", and "/a/../../b" refers to "/b".

(define (bottom-hierarchy path)
  (let1 spath (simplify-page-path path)
    (last
     (string-split
      (if (string=? "/" (string-take-right spath 1))
          (string-drop-right spath 1)
          spath) #\/))))

(define (upper-hierarchy path)
  (let* ((spath (simplify-page-path path))
         (uhier
          (string-join
           (drop-right*
            (string-split
             (if (string=? "/" (string-take-right spath 1))
                 (string-drop-right spath 1)
                 spath) #\/) 1) "/")))
    (if (string-null? uhier) "/" uhier)))

(define (absolute-page-path? path)
  (string-prefix? "/" path))

(define (simplify-page-path path)
  (define (cancel-top xs)
    (if (null? xs) xs (cdr xs)))
  (define (iter ac xxs)
    (if (null? xxs)
        (reverse ac)
        (receive (x xs)
                 (car+cdr xxs)
                 (cond ((string=? x ".") (iter ac xs))
                       ((string=? x "..") (let1 new-ac (cancel-top ac)
                                            (if (null? new-ac)
                                                (append ac xxs)
                                                (iter new-ac xs))))
                       (else (iter (cons x ac) xs))))))
  (let ((hd (if (char=? #\/ (string-ref path 0)) "/" ""))
        (tl (if (char=? #\/ (string-ref path (- (string-length path) 1)))
                "/" "")))
    (string-append hd 
                   (string-join
                    (iter '()
                          (filter (compose not string-null?)
                                  (string-split path #\/))) "/")
                   tl)))

;; Get canonical (absolute) pathname of the page.
;; Returns #f if pagename is invalid.
(define (canon-pagename pagename . current)
  (define (fix-up path) ;; strips leading "/.."
    (cond ((string-null? path) "/")
          ((string-prefix? "/.." path) (fix-up (string-drop path 3)))
          ((string-suffix? "/" path) (string-drop-right path 1))
          (else path)))
  (define (canon-path path . more-paths)
    (fix-up (simplify-page-path (apply build-path path more-paths))))
  (if (absolute-page-path? pagename)
    (canon-path pagename)
    (and-let* ((curpage (get-optional current
                                      (cond ((current-page)
                                             => (cut ref <> 'key))
                                            (else #f)))))
      (cond ((equal? pagename "$") curpage)
            ((string-prefix? "$/" pagename)
             (canon-path curpage (string-drop pagename 2)))
            (else
             (canon-path (upper-hierarchy curpage) pagename))))))

;; Base name to be used for display
;;  NB: it is arguable that whether we should display the pathname
;;  component if the pointed page is in different "directory".
;;  For now, I just take the basename (except the "/").
(define (base-pagename pagename)
  (if (equal? pagename "/") "/" (bottom-hierarchy pagename)))

;; "/aa/bb/cc" -> '("aa" "bb" "cc")
;; "/" -> '()
(define (pagename-split pagename . opts)
  (let1 c (canon-pagename pagename)
    (and c (if (or (string-null? c) (string=? c "/"))
             '()
             (string-split (string-drop c 1) #\/)))))

;; '("aa" "bb" "cc") => "/aa/bb/cc"
;; '() => "/"
(define (pagename-join components)
  (string-append "/" (string-join components "/")))

;; Formatting wikiname.
(define (format-wikiname name)
  (receive (path linkname)
      (cond ((#/^\/{3}(.+)$/ name) => (lambda (m) (values "/" (m 1))))
	    (else                     (string-scan name "//" 'both)))
    (let ((absname  (canon-pagename (or path name)))
          (linkname (or linkname (base-pagename name))))
      (if absname
	  (if (or (lookup-virtual-page absname)
		  (page-data-exists? absname))
	      `((a/cont (@@ (cont ,show ,@(pagename-split absname)))
			,linkname))
	      `((span (@ (class "new-page-link"))
		      ,linkname
		      (a/cont (@@ (cont ,edit ,@(pagename-split absname)))
			      "?"))))
	  (list #`"[[,name]]")))))

;;========================================================
;; Page formatters
;;

;; +---------------------------------------------------+
;; |navigation-pane                  [login]  [search] |
;; +---------------------------------------------------+
;; |title-pane    Page Title                           |
;; +---------------------------------------------------+
;; |side-pane |   edit-menu (only when login)          |
;; |          |----------------------------------------|
;; |          |   contents...                          |
;; |          |                                        |
;; :          :                                        :
;; |          |                                        |
;; +---------------------------------------------------+
;; |                   bottom-pane                     |
;; +---------------------------------------------------+

(define standard-page-template
  (kahua:make-xml-template
   (kahua-template-path "kahua-web" "standard-page.xml")))

(define (standard-fmt-time t)
  (sys-strftime "%Y-%m-%d %H:%M:%S(%z)" (sys-localtime t)))

(define (standard-page page)

  (define (footer page)
    `((div (@ (id "page-footer"))
           ,@(cond-list
              ((ref page 'ctime)
               => (lambda (t) (format "Created: ~a" (standard-fmt-time t))))
              ((ref page 'cuser) => (cut format " by ~a" <>))
              (#t '(br))
              ((ref page 'mtime)
               => (lambda (t) (format "Modified: ~a" (standard-fmt-time t))))
              ((ref page 'muser) => (cut format " by ~a" <>))))))

  (define (edit-menu page)
    (if (kahua-web-authorized?)
	(let1 path (pagename-split (ref page 'key))
	  `((div (@ (id "edit-menu"))
		 (a/cont (@@ (cont ,edit ,@path)) "[Edit]")
		 (a/cont (@@ (cont ,history ,@path)) "[History]"))))
	'()))

  (parameterize ((current-page page))
    (kahua:xml-template->sxml
     standard-page-template
     :title (title/ (string-join (reverse (pagename-split (slot-ref page 'key))) " | " 'suffix)
		    *kahua-web-title*)
     ;; FIXME!!
     :extra-header-elements (if (slot-exists? page 'extra-head-elements)
				(let1 els (slot-ref page 'extra-head-elements)
				  (if (null? els)
				      '(node-set)
				      els))
				'(node-set))
     :rss-link (link/ (@/ (rel "alternate") (type "application/rss+xml")
			  (title (string-append *kahua-web-title* " RSS"))
			  (href (kahua-web-rss-uri))))
     :style-link
     (node-set/ (link/ (@/ (rel "stylesheet") (type "text/css")
			   (href (kahua-static-document-url "kahua-web" "kahua-web.css"))))
		(link/ (@/ (rel "stylesheet") (type "text/css")
			   (href (kahua-static-document-url "kahua-web" "kahua-web-local.css")))))
     :title-pane-contents
     (let1 t (use-page (or (slot-ref page 'title-pane) "/$admin/title-pane"))
       (if (null? t)
	   (h1/ (if (front-page? page)
		    *kahua-web-title*
		    (slot-ref page 'title)))
	   t))
     :navigation-pane-contents (page-navigation page)
     :side-pane-contents (use-page (or (slot-ref page 'side-pane) "/$admin/side-pane"))
     :bottom-pane-contents (use-page (or (slot-ref page 'bottom-pane) "/$admin/bottom-pane"))
     :access-tracker (google-analytics-tracker/)
     :main-pane `((div (@ (id "main-pane"))
		       ,@(if (persistent-page? page) (edit-menu page) '())
		       ,@(wiliki:format-content page)
		       (span (@ (id "footnote")))
		       ,@(if (persistent-page? page) (footer page) '())))
     )))

(define (page-navigation page)
  ;; ("/" "a" "b") => (() ("a") ("a" "b"))
  (define (navi-paths pathl)
    (map-with-index (lambda (ind _) (take pathl ind)) (cons #f pathl)))
  ;; "/a/b" => ["Home" ">" "a" ">" "b"] with appropriate links
  (define (navi-links pathl)
    (intersperse " > "
                 (map (lambda (paths name)
                        (if (get-page (pagename-join paths))
                          `(a/cont (@@ (cont ,show ,@paths)) ,name)
                          name))
                      (navi-paths pathl)
                      (cons "Home" pathl))))
  ;; 
  (define (navigation-right)
    `(form/cont 
      (@@ (cont ,search)) (@ (class "search"))
      ,@(login-link) " " ,(make-submit "Search:") ,(make-textbox "word" '(size 10))))
  ;; body
  `((table
     (tr (td (@ (id "navigation-pane-left"))
             ,@(navi-links (pagename-split (ref page 'key))))
         (td (@ (id "navigation-pane-right"))
	     ,(navigation-right))))))

(define (get-source page-path)
  (or
   (and-let* ((page (get-page page-path))
	      (content (ref page 'content)))
     (if (pair? content)
	 (format "{{{\n~a\n}}}\n" content)
	 (string-concatenate 
	  (map (lambda (line) (format " ~a\n" line))
	       (string-split (ref page 'content) "\n")))))
   (format "* No Such Page: ~a" page-path)))

(define-entry (source :rest page-name)
  (let ((page-path (pagename-join page-name)))
    (standard-page
     (make <page> :title (format "Source of ~a" page-path)
	          :key   (format "/Source/~a" page-path)
                  :content (get-source page-path)))))

(define (redirect-to entry-name . args)
  `((html (extra-header (@ (name "Status") (value "302 Found")))
	  (extra-header (@ (name "Location")
			   (value ,(apply kahua-self-uri-full entry-name args)))))))

(define-method redirect-to ((page <page>))
  (apply redirect-to "show" (map uri-encode-string
				 (pagename-split (slot-ref page 'key)))))

(define-method redirect-to ((page <boolean>))
  (redirect-to (get-page "/")))

(define (format-time t)
    (sys-strftime "%Y/%m/%d %H:%M:%S" (sys-localtime t)))

(let ((formatter (wiliki:formatter)))
  (set! (ref formatter 'bracket) format-wikiname)
  (set! (ref formatter 'macro) format-macro)
  (set! (ref formatter 'time) format-time)
  )

;; A common routine to format a customizable pane
(define (use-page pagename)
  (cond ((get-page pagename) => wiliki:format-content)
        (else (format-wikiname pagename))))

;;========================================================
;; Main application
;;

(define-entry (show :rest page-path)
  (let ((pagename (pagename-join page-path)))
    (cond ((get-page pagename) => standard-page)
	  ((equal? "/" pagename)
	   (standard-page (make <page>
			    :title "Top Page"
			    :key "/" :content (format-wikiname "/"))))
          (else (standard-page (make-nonexistent-page pagename))))))

;; assumes pagename is canonicalized
(define (make-nonexistent-page pagename)
  (make-error-page "Error: nonexistent page"
                   `(p "Page \"" ,pagename "\" does not exist.")))

(define (make-error-page title body)
  (make <page> :title title :key "/Error" :content body))

(define (error-proc exc)
  (if (kahua-web-error? exc)
      (ref exc 'form)
      (standard-page
       (make-error-page "Error: program error"
			`(,@(use-page "/$admin/program-error")
			  (pre ,(kahua-error-string exc (kahua-current-user))))))))

(define-entry (edit :rest page-path)
  (let ((pagename (pagename-join page-path)))
    (cond ((get-page pagename) => edit-page)
          (else (edit-page (make <page> :title pagename :key pagename))))))

(define (edit-page page . extra-contents)
  (define commit
    (entry-lambda (:keyword content donttouch addtonews
                            changename confirmchange comment
			    title-pane side-pane bottom-pane)
      (define (string* str)
	(and str
	     (not (string-null? (string-trim-both str)))
	     str))
      (slot-set! page 'content (canon-content content))
      (slot-set! page 'title-pane (string* title-pane))
      (slot-set! page 'side-pane (string* side-pane))
      (slot-set! page 'bottom-pane (string* bottom-pane))
      (and-let* (((equal? confirmchange "on"))
                 (newname (canon-pagename changename)))
        (slot-set! page 'key newname))
      (save-page page
                 (lambda (page)
                   (unless (equal? donttouch "on")
                     (page-set-push-unique! (page-set:recent-changes) page))
                   (when (equal? addtonews "on")
                     (page-set-push-unique! (page-set:news) page))
		   (run-hook save-page-hook)
                   (redirect-to page))
                 comment)))
  (with-kahua-web-authorization
   (lambda ()
     (let* ((admin-page? (#/^\/\$admin(?:\/.+)?$/ (slot-ref page 'key)))
	    (epage (make <page>
		     :title #`"Editing ,(ref page 'title)" :key (ref page 'key)
		     :content
		     `(,@extra-contents
		       (div
			(@ (class "edit-inputs"))
			(form/cont
			 (@@ (cont ,commit)) (@ (class "page-editor"))
			 ,(make-textarea "content" (slot-ref page 'content)
					 '(rows 30) '(cols 80))
			 (br)
			 (div "Log Message: " ,(make-textbox "comment" '(size 60)))
			 ,(make-submit "Commit")
			 (br)
			 (div
			  (label "Title Pane: "
				 ,(make-textbox "title-pane"
						`(value ,(slot-ref page 'title-pane))
						'(size 50)))(br)
			  (label "Side Pane: "
				 ,(make-textbox "side-pane"
						`(value ,(slot-ref page 'side-pane))
						'(size 50)))(br)
			  (label "Bottom Pane: "
				 ,(make-textbox "bottom-pane"
						`(value ,(slot-ref page 'bottom-pane))
						'(size 50)))(br)
			  ,(make-checkbox "donttouch" "on" "Don't update 'Recent Changes'"
					  admin-page?)
			  " "
			  ,(make-checkbox "addtonews" "on" "Add to news item" #f)
			  (br)
			  (label "Change Page Name : "
				 ,(make-textbox "changename" `(value ,(slot-ref page 'key))
						'(size 20)))
			  " "
			  ,(make-checkbox "confirmchange" "on" "I'm sure" #f
					  '(id "confirmchange")))
			 (br)
			 (div (@ (class "edit-help"))
			      ,@(use-page "/$admin/edit-help"))
			 
			 ))))))
       (standard-page epage)))))

;; opml

(define (page->opml . prefix)
  (define (name-of data) (ref data 'name))
  (define (content-of data) (ref data 'content))
  (define (render forest)
    `(,@(map (lambda (tree)
	       `(item (@ (text ,(name-of (car tree)))
			 (content ,(content-of (car tree))))
		      ,@(if (null? (cddr tree)) '()
			    (render (cddr tree)))))
	     (sort forest (lambda (a b) (string<? (name-of (car a)) (name-of (car b)))))))
    )
  (define (find-parent me forest)
    (find (lambda (x)
            (and (not (eq? me x))
                 (string-prefix? #`",(name-of (car x))/" (name-of (car me)))
                 (not (string-scan (string-drop (name-of (car me))
                                                (+ (string-length (name-of (car x))) 1))
                                   #\/))))
          forest))
  (let1 page-data  ;; ((name root? . children) ...)
      (filter-map (lambda (data)
                    (and (string-prefix? (pagename-join prefix) (name-of data))
                         (list data #t)))
                  (filter (lambda (x) (and (string? (name-of x))
					   (string-prefix? (pagename-join prefix) (name-of x))))
			  (all-page-data)))
  `(*TOP* (*PI* xml "version=\"1.0\" encoding=\"UTF-8\"")
	  (opml (@ (version "1.0"))
		(head)
		(body
		 ,@(let loop ((pages page-data))
		     (cond
		      ((null? pages) (render (filter (cut cadr <>) page-data)))
		      ((find-parent (car pages) page-data)
		       => (lambda (parent)
			    (set! (cadr (car pages)) #f)
			    (push! (cddr parent) (car pages))
			    (loop (cdr pages))))
		      (else (loop (cdr pages)))))
		 )))))

(define-entry (opml :rest prefix)
  (let* ((body (apply page->opml prefix))
	 (content (srl:sxml->xml body))
	 (fname #`"opml.xml"))
    (receive (out filename)
	(sys-mkstemp (build-path (kahua-temporary-directory) "opml"))
      (let1 utf-out (wrap-with-output-conversion out "UTF-8")
	(display content utf-out)
	(close-output-port utf-out)
	(send-downloaded-file filename fname)))))


;; display version
(define-entry (version)
  (with-kahua-web-authorization
   (lambda ()
     (let* ((content `((table
			(@ (class "version-info"))
			(tr (th "Kahua-Web version: ") (td ,*kahua-web-version*))
			(tr (th "Kahua version:") (td ,(kahua-version)))
			(tr (th "Gauche version:")
			    (td ,(gauche-version)
				" ["
				,(symbol->string (gauche-character-encoding))
				","
				,(symbol->string (gauche-thread-type))
				"]")))))
	    (vpage (make <page> :title "Kahua-Web version"
			 :key "/Version" :content content)))
       (standard-page vpage)))))

;; canonicalize EOL to #\newline
(define (canon-content content)
  (string-join (call-with-input-string content port->string-list)
               "\n" 'suffix))

;; Support for Google Analytics Tracking
(define (google-analytics-tracker-urchin/)
  (when/ (*google-analytics-tracking*)
    (node-set/
     (script/ (@/ (src "http://www.google-analytics.com/urchin.js")
		  (type "text/javascript")))
     (script/ (@/ (type "text/javascript"))
	      #`"_uacct=\",(*google-analytics-tracking*)\";\n"
	      "urchinTracker();"))))

(define (google-analytics-tracker-ga/)
  (when/ (*google-analytics-tracking*)
    (let1 scheme+host (if (kahua-meta-ref "HTTPS") "https://ssl." "http://www.")
      (node-set/
       (script/ (@/ (type "text/javascript")
		    (src #`",|scheme+host|google-analytics.com/ga.js")))
       (script/ (@/ (type "text/javascript"))
		#`"var pageTracker = _gat._getTracker(\",(*google-analytics-tracking*)\");\n"
		"pageTracker._trackPageview();\n")))))

(define google-analytics-tracker/ google-analytics-tracker-ga/)

;;========================================================
;; initialization
;;
   
(initialize-main-proc show)

;;(kahua-error-proc error-proc)
